### Синтаксис KayJam

#### Начальная информация
Весь код в KayJam пишется через **точку с запятой** ``;``, 
но в некоторых случаях её писать не обязательно 
(например перед закрывающей волнистой скобкой ``}`` или перед ``else`` в ``if``)

```
var test = 123;
if(test)
    return test //Писать ; перед else не нужно
else return false;
```

#### Контейнеры
Контейнеры - это конструкции, в которых содержится некоторый код.
Самая базовая - ``{}``. В базовых конструкциях все переменные передаются 
и сохраняются.

```
var test = true;
{
    var test1 = true;
    test = false;
}

// test будет false и переменная test 1 будет создана
```

А вот в некоторых нет. Например в функциях, объектах, классах переменные 
не передаются другим контейнерам. Они ограничены областью видимости.

#### Создание переменной
Чтобы **создать** переменную, нужно написать ``var`` и после него название.
И нужно присвоить значение через ``=``. 
В одной области видимости переменная может быть создана **только один раз**.

Для изменения переменных используется другая конструкция! (см. ниже)

```
var test = "Hello world!";
var test = 123; //Выведет ошибку

fun test(){
    var test = 123; //Ошибки не будет
}
```

#### Изменение переменных
Чтобы изменить уже созданную переменную, нужно использовать конструкцию,
как у создания переменной, но уже без var

```
var test = "Hello world!";
test = 123; // Ошибки не будет

fun test(){
    test = 123; //Выведет ошибку
}
```

#### if (если) конструкция
if конструкция работает так:
если условие не равно false, то выполняется код после if;
если равно false, то выполнится код после else (если он указан).

```
var code = false;
if(code){
    return 1; //Этот код никогда не выполнится
}else{
    return 2; //А этот код выполнится
}
```

Также этот код может проверить и создать переменную, если она не равна false:

```
if(var code = true){
    return code; //Выведет true
}
```

С помощью этой конструкции можно присвоить (создать) выражение:

```
var boolVar = if(true) 
    return "Hello World"
else return false;
```

#### Отрицание (инвертация)
С помощью этой конструкции можно превратить false в true и наоборот.
Например `!false` выведет `true`, а `!true` выведет `false`.

#### Функции
С помощью функций вам не обязательно писать один и тот же код несколько раз.
Вы можете вынести некоторый код и вызывать когда вам нужно.

Чтобы написать функцию, напишите ``fun`` или ``function`` и название функции.
Дальше откройте скобку, перечислите аргументы (``тип название``) и закройте скобку.
И напишите, открыв волнистую скобку `{`, сам код. 

```
function test(string str) {
    println(str);
}

fun test(string str) {
    println(str);
}
```

Также вы можете указать тип возврата функции. Для этого нужно после 
перечисления аргументов поставить двоеточие, и написать тип. 
По умолчанию тип возврата void. То есть функция ничего не возвращает.

```
fun test(string str): any {
    return str;
}

fun test(string str): int {
    return str; //Выведет ошибку
}
```

Чтобы вызвать функцию, вы должны написать имя функции и, открыв скобку,
написать аргументы:

```
var test = test("test");

fun test(string str): any {
    return str;
}
```

#### Объекты
Объекты - это контейнеры, которые содержат в себе переменные и функции.
Их можно переносить в других переменных. 
Из них можно брать переменные и вызвать из них функции.

Чтобы создать объект, достаточно написать `object` и, открыв волнистую скобку,
записать "внутренности" объекта

```
var testObj = object {
    var test = 100;

    fun test(int test): int{
        return test;
    }
};

testObj.test(testObj.test);
```

#### Классы
> Раздел в разработке

Классы - это объекты, но в отличие от них класс можно записать один раз 
и создавать из него объекты. 

Чтобы создать класс, нужно написать `class`, далее имя класса и, открыв волнистую скобку,
записать "внутренности" класса.

```
class A {
    var testVar = 123;

    fun test(): int{
        return this.testVar;
    }
}
```

Также можно наследовать функции и переменные другого класса. 
Нужно после имени класса добавить двоеточие `:` и написать имя класса, которого хотите наследовать.

```
class A {
    var testVar = 123;

    fun test(): int {
        return this.testVar;
    }
}

class B {
    fun getTest(): any {
        return this.test();
    }
}
``` 

Чтобы при создании экземпляра класса можно было передать 
или выполнить некоторые функции,
нужно написать в классе конструктор. Конструктор записывается так:
пишем внутри класса `constructor` и, открыв скобку, пишем аргументы.
После того как аргументы написаны, нужно открыть закруглённую скобку
и записать действия, которые нужно сделать.

```
class A {
    var testVar = 0;

    constructor(int testVar){
        this.testVar = testVar;
        //Еще какие-то действия
    }

    fun test(): int {
        return this.testVar;
    }
}
```

Чтобы создать экземпляр класса, нужно написать имя класса и 
аргументы одного из его конструктора. То есть, как и вызов функции.

```
class A {
    var testVar = 0;

    constructor(int testVar){
        this.testVar = testVar;
    }

    fun test(): int {
        return this.testVar;
    }
}

var a = A(123);

return a.test();
```

#### Лямбда функции или же анонимные функции
Лямбда функции (анонимные функции) - это функции,
которые могут содержаться в переменных. 

Чтобы записать её, нужно написать `->` и, открыв скобку,
перечислить аргументы. Потом можно написать любую конструкцию,
или же открыть контейнер и записать в ней код.

```
var lambdaFun = -> (test) {
    return test;
};

return lambdaFun(123);
```

#### Списки (array) 
> Пока что в разработке.

Чтобы создать список, нужно написать квадратную скобку `[`
и перечислить в ней значения. Когда все значения перечислены, 
нужно закрыть квадратной скобкой `]`.

```
var test = [1,2,3,"test"];
```

Из списка можно получить значение следующим образом:
```
var test = [1,2,3,"Hello World!"];
var helloWorld = test[3]; 
//Мы таким образом достаём четвёртое 
//значение из списка
//3 указано, потому что список начинается с нуля
//То есть test[0] будет самым первым значением в списке
```

#### На этом всё!
Теперь вы знаете основы синтаксиса KayJam.
Также вы можете посмотреть встроенные функции KayJam на 
[этой](https://github.com/KayJamLang/core/blob/main/docs/ru/functions.md) странице.

